\chapter{Langton's Ant}\label{chp-langtons-ant}

This program is something I remember writing many years ago when I was working in Aberdeen,which was some time in the 11 years prior to 1996 when I moved South of the Border, to deprive an Englishman of a woman and a job!\footnote{Joke!}

Back then, I wrote it in C using \emph{Borland Turbo C++ Version 3.0} - probably the first program I ever wrote in C on a PC.

Anyway, it's a demonstration of something called \emph{Emergent Behaviour} which is something that emerges from some set of rules, but what emerges \emph{was not specifically} programmed into the rules.

The way birds flock together, but manage not to crash into one another, for example, is based on a couple of rules

\begin{itemize}
	\item Fly in the same direction as your neighbour;
	\item Don't fly too close nor too far away;
	\item Don't fly into things.
\end{itemize}

(Or something similar). A program was written some years ago that embodied those rules and the result was a flock of ``boids'' as the program was called. It's quite famous.

Anyway, Langton's Ant is another one of those, it has only two rules:

\begin{itemize}
	\item If you are on a white cell, colour it black, turn right, step forward;
	\item If you are on a black cell, colour it white, turn left, step forward
\end{itemize}

That's all there is to it.

\subsection{The Program Listing}

I coded this program in SuperBASIC first of all, just to get my head around the algorithm. When run on QPC on my 10 year old laptop, it ran pretty damned quickly. And yes, the emergent behaviour was there after about 10,000 steps, as expected.\footnote{See the Wikipedia article at \href{https://en.wikipedia.org/wiki/Langton's\_ant}{https://en.wikipedia.org/wiki/Langton's\_ant}}

Anyway, I then coded it in Assembly and was wondering just how much faster it would be. The answer? Pretty damned fast. I had to slow it down by inserting a one frame suspension of the job which actually made it run slower than SuperBASIC! But that was then too slow, so I ended up with a delay loop\footnote{And delay loops burn CPU cycles and keep the computer busy where suspending the job would release resources and let other applications run better.} instead.

\begin{lstlisting}[firstnumber=1,caption={Langtons Ant - Opening Blurb}]
;--------------------------------------------------------------------
; LANGTON'S ANT
;
; A small routine to demonstrate how emergent behaviour can, ahem,
; emerge from simple rules.
;
; See Wikipedia: https://en.wikipedia.org/wiki/Langton's_ant
;
; The ant lives in a world which is a certain size wide and deep and
; the ends wrap around. It starts off in the middle, and walks in a
; downward direction - it makes no difference though, the behaviour
; will still emerge.
;
; If the ant lands on a cell which is white, it must turn it black,
; turn right, and walk on to the next cell in that new direction.
;
; If the cell was black, the ant must turn it white, turn left, and
; walk on to the next cell in the new direction.
;
; That is all the rules. Run the program and see what happens!
;--------------------------------------------------------------------
; Norman Dunbar
; 17 February 2018.
;
; This source code is open source. Use it as you see fit and if you
; improve it, tell everyone and let them have the new source!
;--------------------------------------------------------------------
\end{lstlisting}

As ever, the code above introduces the program. Not much to see here, but there is a reference to the Wikipedia article about Langton's Ant.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Equates}]
;--------------------------------------------------------------------
; The sizes must be a power of two. We need this later when we limit
; them to anything between 0 and the size minus 1. If they are not a
; power of two, it doesn't work the way I've done things later!
;--------------------------------------------------------------------
size_x
           equ     512                 ; Pixels across (width)
size_y
           equ     256                 ; Pixels down (height)

;--------------------------------------------------------------------
; Color names, better than guessing the colour values. The ESC key is
; defined here, or its bit number in D1 after an IPC call.
;--------------------------------------------------------------------
black
           equ     0                   ; Black cell colour
red
           equ     2                   ; It's a red ant!
white
           equ     7                   ; White cell colour
green
           equ     4                   ; The world colour
esc
           equ     3                   ; Bit number for ESC
\end{lstlisting}

The next section of code, above, are the equates that are needed. Please note that the two sizes, \opcode{size\_x} and \opcode{size\_y} define the maximum width and height of the world in which the ant lives. They must be a power of two because if they are not, bad stuff will happen later. 

Various colours are also defined here, plus the bit that we need to check to see if there was a press of the \opcode{ESC} key.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Job Header}]
;--------------------------------------------------------------------
; Standard Job start.
;--------------------------------------------------------------------
start
           bra.s   Langton
           dc.l    0
           dc.w    $4afb

name
           dc.w    nameEnd-name-2
           dc.b    "Langton's Ant"
nameEnd
           equ     *

\end{lstlisting}

The code above is the standard QDOSMSQ job header. You should be getting used to seeing these by now!

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Command and Channel Definitions}]
;--------------------------------------------------------------------
; BLOCK command parameter block.
;--------------------------------------------------------------------
Block
           dc.w    1,1
xPos
           dc.w    Size_x/2,Size_y/2   ; 256, 128 initial coordinates

;--------------------------------------------------------------------
; Check ESC key pressed IPC command string.
;--------------------------------------------------------------------
ipc_command
           dc.b    9,1,0,0,0,0,1,2     ; KEYROW(1)

;--------------------------------------------------------------------
; Screen channel name and window definition block.
;--------------------------------------------------------------------
scr_def
           dc.w    4
           dc.b    'scr_'              ; No input needed, so SCR_

winDef
           dc.w    size_x
           dc.w    size_y
           dc.w    (512-size_x)/2      ; Assumes 512 max width
           dc.w    (256-size_y)/2      ; Assumes 256 max depth

\end{lstlisting}

The section of code above sets up a few commands, the first is the \opcode{BLOCK} command to draw a 1 pixel by 1 pixel block in the middle of the ant's world.

Next to that is a command to allow the IPC processor to read the keyboard, specifically the equivalent of \opcode{KEYROW(1)} which is where we find the \opcode{ESC} key.

Finally, we have the definition for a \opcode{SCR\_} channel and a block of words to redefine the window we will set up on that channel. We do it this way because we only need to set the sizes of the ant's world, and hopefully, the assembler will work out the window definition for us. Lazy? Me?

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Trap Subroutines}]
;--------------------------------------------------------------------
; Subroutines to do a trap, test D0 and die horribly if there was
; an error.
;--------------------------------------------------------------------
doTrap1
           trap    #1
           bra.s   testD0

doTrap2
           trap    #2
           bra.s   testD0

doTrap3
           trap    #3

testD0
           tst.l   d0
           bne.s   suicide
           rts

suicide
           move.l  d0,d3
           moveq   #mt_frjob,d0
           moveq   #-1,d1
           trap    #1


\end{lstlisting}

The code above is a set of three simple subroutines to execute a \opcode{trap} instruction, check the error return and to kill the job if an error occurred.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Start Here}]
;--------------------------------------------------------------------
; The main starting place. Open a scr_ channel. The channel ID will
; stay protected in A0.L throughout the rest of the code.
;--------------------------------------------------------------------
Langton
           lea     scr_def,a0          ; Screen channel
           moveq   #io_open,d0
           moveq   #-1,d1
           moveq   #0,d3
           bsr.s   doTrap2

;--------------------------------------------------------------------
; The screen channel is open, make the window the requested size.
;--------------------------------------------------------------------
Window
           moveq   #sd_wdef,d0         ; Window definition
           moveq   #0,d1
           moveq   #0,d2
           moveq   #-1,d3
           lea     winDef,a1
           bsr.s   doTrap3

;--------------------------------------------------------------------
; Set the paper to green.
;--------------------------------------------------------------------
Paper
           moveq   #sd_setpa,d0        ; Set paper to green
           moveq   #green,d1
           bsr.s   doTrap3

;--------------------------------------------------------------------
; And clear the entire screen.
;--------------------------------------------------------------------
Cls
           moveq   #sd_clear,d0        ; Cls
           bsr.s   doTrap3

\end{lstlisting}

This is the beginning of the real code. We open the channel to the screen, redefine it using the window definition block above, which has been filled in with the appropriate sizes by the assembler, set the paper to green and clear the screen.

Easy stuff, but anyone following \href{http://qlforum.co.uk}{http://qlforum.co.uk} will know that I had a few problems and needed help. I was calling the wrong \opcode{trap} instructions, so instead of opening a channel, I was creating a weird job instead. Not good when you try to set paper colour on a job id rather than a channel id!

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Initialise the World}]
;--------------------------------------------------------------------
; The cells bitmap defines the ant's world. A bit is clear for white
; or set for black. Initially, all cells are white. It makes no
; difference - try setting the cells to FF (all black) and you'll see
; the same results.
;--------------------------------------------------------------------
clearCells
           lea     cells,a3            ; The ant's world lives here
           move.l  a3,a2               ; Use A2 for the loop
           moveq   #0,d0               ; Byte counter
           move.w  winDef(pc),d0       ; Get width in pixels
           lsr.w   #3,d0               ; Calculate width in bytes
           mulu    winDef+2(pc),d0     ; Times height in pixels
           bra.s   clearNext

clearLoop
           clr.b   (a2)+               ; Clear one byte

clearNext
           dbra    d0,clearLoop        ; And the rest


\end{lstlisting}

The ant's world is determined by a bitmap where one bit represents the white or black colour of a cell within the world. The code above clears out the bitmap to ensure that no random bits are to be found. The ant's world is coloured white at the start.

However, feel free to change this to black and fill each byte with \$$FF_{hex}$ instead, if you wish. It is the rules that determine the behaviour.

It should be noted that the size of the ant's world should not exceed a word sized register when multiplying. The calculation is $height * (width / 8)$ because each byte represents 8 pixels across.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Initialising Constants}]
;--------------------------------------------------------------------
; Initialise A2 to point at the block command parameters. We need it
; in A1 actually, but A1 gets corrupted so we save it in A2. We also
; load up the initial position of the ant into D2. The top word of D2
; is the Across coordinate, the low word is the Down coordinate. D5
; is set to zero, for DOWN, which is the direction the ant is facing.
; Try changing it, see what happens.
; A5 is set to the IPC command string, which we need in A3 later. So
; as A3 is preserved through an IPC call, we can EXG these two.
;--------------------------------------------------------------------
           lea     Block,a2            ; Needed in A1 later
           move.l  xpos(pc),d2         ; Hi = Across, Lo = Down
           moveq   #0,d5               ; Direction ant is walking
           lea     ipc_command,a5      ; Read KEYROW 1 for ESC key

\end{lstlisting}

The code above initialises some constants within the program. \opcode{A2} which is never corrupted throughout the program, holds the address of the \opcode{BLOCK} command. We actually need that in \opcode{A1} but that gets corrupted in places, so \opcode{A2} is a good place to keep it safe.

\opcode{D2} is initialised with the initial coordinates which have been set to be half the width and half the height of the ant's world. The upper word of \opcode{D2} is the x or \emph{across} coordinate and the lower word is the y or \emph{down} coordinate. 

\opcode{D5} holds the direction that the ant is facing. It is initially facing down, but this can be changed if you wish, make sure you set it to a value between zero and three though.

Finally, \opcode{A5} holds the address of the IPC command to read the keyboard. This is actually something we need in \opcode{A3} but that is needed elsewhere, so \opcode{A5} was a handy place to keep it. We will swap those two registers over as and when we need to.

Talking of registers...

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Register Usage}]
;--------------------------------------------------------------------
; REGISTERS USED BEYOND THIS POINT
;
; D0 = Trap codes.
; D1 = Ant or Cell colour, red, white or black.
; D2 = Hi word = Across, Lo word = Down coordinates.
; D3 = Timeout for traps. -1 always.
; D4 = Bit number within Cells bitmap, for the current pixel.
; D5 = Direction indicator. 0=Down, 1=Left, 2=Up, 3=Right.
; D6 = Used to calculate A4 and D4 each time through the loop.
; D7 = Copy of Direction, used in walking to next cell.
;
; A0 = Channel Id
; A1 = Used for trap calls.
; A2 = Pointer to BLOCK command in job code. (Used in A1.)
; A3 = Pointer to Cells bitmap.
; A4 = Pointer to byte in Cells where current coordinates is found.
; A5 = Pointer to IPC command to read KEYROW 1 for ESC key.
; A6 = Reserved for QDOS/SMSQ
; A7 = Stack Pointer.
;
; Cells Bitmap.
;
; The cells array of bytes measures size_y * (size_x / 8) bytes
; and holds 1 bit for every cell in the screen (512 * 256 default)
; with a zero bit meaning a white cell and a 1 bit being black.
;--------------------------------------------------------------------

\end{lstlisting}

The comments above show the usage of the registers in the remainder of the program. As you can see, I'm just about using all of them for one thing or another.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Start of Loop}]
;--------------------------------------------------------------------
; And here we start the main loop. We adjust the ant coordinates to
; fall into range. This is why we used a power of two as the sizes as
; it makes it easier to keep the coordinates in range 0 to size - 1.
;--------------------------------------------------------------------
antWalk
           andi.w  #size_y-1,d2        ; Make down 0 to size_y-1
           swap    d2
           andi.w  #size_x-1,d2        ; Make across 0 to size_x-1
           swap    d2
           move.l  d2,4(a2)            ; Update block coordinates

;--------------------------------------------------------------------
; We have saved the adjusted coordinates in the BLOCK command's 
; parameters, plot the current ant position with a red pixel. Good
; luck seeing that, it moves pretty quickly!
;--------------------------------------------------------------------
plotAnt
           moveq   #sd_fill,d0         ; Block command
           moveq   #red,d1             ; It's a red ant of course!
           move.l  a2,a1               ; Block parameter block
           bsr     doTrap3             ; Won't return on error

\end{lstlisting}

This is the code that runs our ant's world. Because we made the width and height of the world a power of two, we can subtract 1 to get a mask that will ensure that it will remain in range and this also helps in wrapping the ant's world around, top and bottom as well as left and right. 

Given that width, for example, is 512, then \opcode{AND}ing the across coordinate with 511 will cause it to be in range 0 to 511 always. If it becomes 512 then and will reset it to 0. If it goes to minus 1, the and will reset it to 511 - and so, we have a simple way of wrapping the coordinates.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Pixel Calculations}]
;--------------------------------------------------------------------
; We need to get the current cell at this point. We want the address
; of the current byte in A4 and the number of the bit in that byte in
; D4. At this point we have:
;
; A3 = the cells bitmap start address.
; D2 = Across | Down coordinates.
;--------------------------------------------------------------------


;--------------------------------------------------------------------
; First work out the bit number in the cell's byte. This is simply
; 7 - (across coordinate MOD 8).
;--------------------------------------------------------------------
bitNumber
           move.l  d2,d6               ; Copy Across | Down coords
           swap    d6                  ; Down | Across
           move.w  d6,d4               ; Copy Across coordinate to D4
           andi.w  #7,d4               ; Mod 8 = 0 to 7
           neg.w   d4                  ; -7 to 0
           addq.w  #7,d4               ; D4.W = bit number

;--------------------------------------------------------------------
; The byte within the row, which we calculate soon, is calculated as
; (across coordinate DIV 8). Easy.
;--------------------------------------------------------------------
byteNumber
           lsr.w   #3,d6               ; Across div 8

;--------------------------------------------------------------------
; The correct row number in the cells bitmap is the Down coordinate
; multipled by (size_x DIV 8). This shouldn't exceed a word size. The
; result is added to A3 which is the start address of the cells
; bitmap and loaded into A4 as the byte address that we want.
;--------------------------------------------------------------------
doRow
           move.w  #size_x,d7          ; Pixels across
           lsr.w   #3,d7               ; Now bytes across
           mulu    d2,d7               ; Times down coordinate
           lea     0(a3,d7.w),a4       ; Address of correct row
           adda.w  d6,a4               ; Correct byte in row

;--------------------------------------------------------------------
; Given the byte address in A4 and the bit number in D4, we can now
; test that individual bit. A zero is white while a 1 is black.
;--------------------------------------------------------------------
           btst    d4,(a4)             ; 0 = white, 1 = black

\end{lstlisting}

The code above takes the two coordinates of the ant's current cell and from them works out which bit in the bitmap represents that cell.

There are four things we need:

\begin{itemize}
	\item The base address of the cells bitmap. This is held in \opcode{A3}.
	\item The correct ``row'' in the bitmap; This will be in \opcode{D7}.
	\item The correct byte within the row; This will be in \opcode{D6}
	\item The correct bit within the byte; This will be in \opcode{D4}.
\end{itemize}

The calculations are reasonably simple:

%%%%%% We need the ~ to get spaces in the following equations. %%%%%%%

$D4 = 7 - (Across~ \text{mod}~ 8)$

$D6 = (Across~ \text{div}~ 8)$

$D7 = Down * (width~ \text{div}~ 8)$

Once we have calculated all those, we need simply to load the byte address into \opcode{A4} and the bit number into \opcode{D4} and then we can test the bit to see if it is black or white, with zero being white.


\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Rule 1}]
;--------------------------------------------------------------------
; Rule 1: If the cell is white, turn it black, right turn, walk on.
;--------------------------------------------------------------------
ruleOne
           bne.s   ruleTwo
           bset    d4,(a4)             ; Turn it black in the bitmap
           moveq   #black,d1           ; Colour for BLOCK command
           subq.b  #1,d5               ; Direction - 1 = right
           bra.s   doDirection         ; Prepare to walk on

\end{lstlisting}

This is the code for rule 1. If the cell that the ant arrived on is white then colour it black and make a right turn by subtracting 1 from the direction. We don't actually colour the cell here, we simply set the cell's new colour, black, in \opcode{D1} ready to be plotted below.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Rule 2}]
;--------------------------------------------------------------------
; Rule 2: If the cell is black, turn it white, left turn, walk on.
;--------------------------------------------------------------------
ruleTwo
           bclr    d4,(a4)             ; Turn it white in the bitmap
           moveq   #white,d1           ; Colour for BLOCK command
           addq.b  #1,d5               ; Direction + 1 = left

\end{lstlisting}

This is the code for rule 2. If the cell that the ant arrived on is black then colour it white and make a left turn by adding 1 to the direction. Again there is no actual colouring here, we simply set \opcode{D1} to be white, ready for later.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - New Direction}]
;--------------------------------------------------------------------
; We have adjusted the direction. Make sure we restrict it to 0 - 3.
;--------------------------------------------------------------------
doDirection
           andi.b  #3,d5               ; Restrict to 0 to 3
           move.b  d5,d7               ; Copy new direction

;--------------------------------------------------------------------
; Ready to walk on in the new direction. 
;
; The directions are:
;
;          UP
;          2
; LEFT 3       1 RIGHT
;          0
;        DOWN
;
; So, if direction is up (2) then turn right = 1 and turn left = 3
;
; Plus 1 = turn left
; Minus 1 = turn right.
;
; If direction = down  = 0 then Down   + 1
; If direction = right = 1 then Across + 1
; If direction = up    = 2 then Down   - 1
; If direction = left  = 3 then Across - 1
;--------------------------------------------------------------------

\end{lstlisting}

Most of the code above is simply comments explaining the directions and how adding or subtracting 1 from the current direction equates to a left or right turn. However, we do have to be careful to make sure that the direction, in \opcode{D5} is restricted to the values 0 through 3 only.

Because we are going to mess about with the new direction value, in the code below, we copy it to a working register, in this case, \opcode{D7}.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Walk On}]
;--------------------------------------------------------------------
; Check the direction for Down, if not then skip, otherwise adjust
; the Down coordinate by +1.
;--------------------------------------------------------------------
doWalk
           bne.s   tryRight

doDown
           addq.w  #1,d2               ; Down + 1
           bra.s   doPlot

;--------------------------------------------------------------------
; Check if the new direction is Right. If so, adjust the Across 
; coordinate by +1.
;--------------------------------------------------------------------
tryRight
           subq.b  #1,d7
           bne.s   tryUp

doRight
           swap    d2                  ; Down | Across
           addq.w  #1,d2               ; Across + 1
           swap    d2                  ; Across | Down
           bra.s   doPlot

;--------------------------------------------------------------------
; Check if the new direction is Up. If so, adjust the Down coordinate
; by -1.
;--------------------------------------------------------------------
tryUp
           subq.b  #1,d7
           bne.s   doLeft              ; Must be left

doUp
           subq.w  #1,d2               ; Down - 1
           bra.s   doPlot

;--------------------------------------------------------------------
; The new direction must be Left. Adjust the Across coordinate by -1.
;--------------------------------------------------------------------
doLeft
           swap    d2                  ; Down | Across
           subq.w  #1,d2               ; Across - 1
           swap    d2                  ; Across | Down

\end{lstlisting}

Because we just \opcode{AND}ed the new direction to keep it in range, we might have set the Z flag. If so, the new direction is Down. In that case we add 1 to the current down coordinate in the low word of \opcode{D2}, and skip over the rest of this code section.

Assuming we are not in the downward direction, we check for a heading towards the right. Subtracting 1 will set the Z flag if we are now facing right. If so, we add 1 to the current across coordinate in the high word of \opcode{D2} and skip the remaining processing.

If we are not facing to the right, are we facing up? Again, subtracting 1 from \opcode{D7} will set the Z flag. If so, we need to subtract 1 from the ant's current down coordinate in register \opcode{D2}'s low word.

Finally, we can only be facing to the left. We have to subtract 1 from the ant's position in the across direction, which is the high word of register \opcode{D2}.

We are now ready to take a step in the new direction as set in \opcode{D2} but first we have to colour the current cell, which happens to be red at the moment, black or white according to register \opcode{D1}.


\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Plot New Cell Colour}]
;--------------------------------------------------------------------
; We can overwrite the red ant at the current coordinates with either
; a black or a white cell. When we get here, D1 holds the correct
; colour.
;--------------------------------------------------------------------
doPlot
           moveq   #sd_fill,d0         ; Block command
           move.l  a2,a1               ; Block parameter block
           bsr     doTrap3             ; Won't return on error

;--------------------------------------------------------------------
; We have overwritten the current coordinate, update the BLOCK 
; parameters with the new coordinates - this is effectively a single
; step in the new direction.
;--------------------------------------------------------------------
nowMove
           move.l  d2,4(a2)            ; Update coordinates

\end{lstlisting}

The code above plots a white or black cell over the red pixel at the ant's original position\footnote{To be honest, the program runs so quick that you almost never see the red ant in the original plot but if you slow it right down, you might see it better.} with a black or white pixel, depending on which of the two rules were activated by the ant's new position.

Once we have plotted the current cell in the correct black or white colour, we store the new coordinates for the ant in the BLOCK command's parameter, ahem, block. This means that when we next pass through the loop, the ant's new coordinates will be plotted in red again, showing the ant's new position.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Checking ESC}]
;--------------------------------------------------------------------
; Read the keyboard and check on the ESC key. If pressed, we are done 
; here. If not pressed, go around again after suspending for a few
; frames.
;--------------------------------------------------------------------
hadEnough
           move.w  d5,-(a7)            ; Direction gets corrupted.
           exg     a3,a5               ; We need the IPC command in A3
           moveq   #mt_ipcom,d0        ; IPC command coming up
           bsr     doTrap1             ; Dies on error
           move.w  (a7)+,d5            ; Restore direction
           exg     a3,a5               ; Cells address in A3 again
           btst    #esc,d1             ; ESC pressed?
           beq     hangAbout           ; No, pause then go around
           moveq   #0,d0               ; Yes, show no errors
           bra     suicide             ; Kill myself

\end{lstlisting}

This section of code checks if the ESC key is being pressed, once around the main loop. Unusually, this call to QDOSSMSQ corrupts register \opcode{D5} so we need to preserve it on the stack during the call. It also helps if you restore it afterwards - which I forgot to do and enjoyed many happy hours debugging a strange bug where the code ran for a while, then simply stopped.

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - Delaying Tactics}]
;--------------------------------------------------------------------
; Delay processing for a few clocks. Without this, the job has pretty
; much finished by the time you get to see the screen!
;--------------------------------------------------------------------
hangAbout  ;bra     antWalk            ; Uncomment for full speed!
           moveq   #3,d0               ; Adjust if too slow/fast

d0Loop     moveq   #-1,d1              ; Inner loop counter

d1Loop     nop                         ; Delay a bit
           nop                         ; Delay a bit more
           dbra    d1,d1Loop           ; End inner loop
           dbra    d0,d0Loop           ; End outer loop           

           bra     antWalk             ; Go around

\end{lstlisting}

When I first wrote this assembly version, I didn't have a delay. The code pretty much finished before the first appearance of the screen so I had to slow it down. 

Initially I suspended the job for 1 frame, but that slowed it down quite a bit, too slow in fact - my SuperBASIC version was much quicker! 

In the end, I slowed it down with a pair of \opcode{NOP}s executed $3 * 65536$ times. If your QL or emulator is too slow (or still too fast) adjust the value in \opcode{D0} to suit.

If you want to see exactly how fast the code is with no delays, uncomment the code at label \opcode{hangAbout} and be amazed!

\begin{lstlisting}[firstnumber=last,caption={Langtons Ant - The Ant's World}]
;--------------------------------------------------------------------
; This is the ant's world. It is a bitmap representing the cells that
; the ant walks over. Each bit defines the colour of a cell. Zero is
; white and 1 is black.
;--------------------------------------------------------------------
cells
           ds.b    size_y*(size_x/8)
\end{lstlisting}

The final part of the listing simply defines a world for the ant. This world consists of one bit per cell and as there are 8 bits (or cells) per byte across the world, we divide by 8 to get the correct number of bytes.

We don't divide for the depth as we need one row per cell in the downward direction.

So that's it. Download the code (or type it in!) from the usual location - \href{http://qdosmsq.dunbar-it.co.uk/downloads/AssemblyLanguage/Issue\_005/}{http://qdosmsq.dunbar-it.co.uk/downloads/AssemblyLanguage/Issue\_005/} - assemble it \program{GWASL}GWASL works fine - and \opcode{EXEC} it. It will run for a bit creating what looks a fairly random pattern on the screen, but then, after about 10,000 iterations of the \opcode{antWalk} loop, it will head off in a straight line for no apparent reason. This is the emergent behaviour from two simple rules.
