
\chapter{Free Pascal Compiler}

Back in November 2020, there was a ``thing'' called \emph{QLvember}\footnote{I really hate it when they do stuff like that, taking a month and
a concept, and putting them together. Things like Dryanuary, Veganuary,
QLvemver -- it makes my teeth crawl! Anyway, enough from the old
git!} and someone -- K\'aroly Balogh is his name, he's from Hungary,
and \emph{Chainq} is his nickname on the QL Forum -- decided to try
and make the Free Pascal Compiler (FPC) work on the QL. After an exchange
of information on QL Forum, \href{https://qlforum.co.uk/viewtopic.php?f=3&t=3057&p=37465\#p37465}{this thread}\footnote{https://qlforum.co.uk/viewtopic.php?f=3\&t=3057\&p=37465\#p37465},
he went off and created a cross compiler for the QL.

Eventually, myself and Marcel got slightly involved and helped create
a starting set of Pascal ``units'' (aka libraries if you wish) to
make writing Pascal easy for QL users. 

Then I got distracted, my wife and I, after 25 years of marriage (almost),
heard the pitter patter of tiny feet! We bought a puppy! Looking after
one of those is hard work -- I'd forgotten how hard it can be --
but he's settled down a bit better now, so I'm getting back into development
again. Just as well I wrote a document detailing how to get hold of
the development tools and source code required to build the compiler
and the QL Units. You can find it on my \href{https://github.com/NormanDunbar/FPC-CrossCompiler-QL/releases/latest/.}{GitHub page, here}\footnote{https://github.com/NormanDunbar/FPC-CrossCompiler-QL/releases/latest/.}.

This article is an attempt to get more people interested enough to
help out writing code for the QL's run time library and other units.
If we can do this, we will have a proper modern, frequently updated
cross compiler for the QL. I'm running FPC on Linux, but it's also
available for Windows too. Either way, we can now cross compile Pascal
code for the QL -- there are restrictions obviously, because the
RTL and Units are incomplete, but if more of us join in, we might
get something done.

Dare I mention, the FPC compiler is able to compile Object Oriented
code, which runs on the QL. I have a post on the \href{https://qlforum.co.uk/viewtopic.php?f=3&t=3725}{forum here}\footnote{https://qlforum.co.uk/viewtopic.php?f=3\&t=3725}
about this. Tony Tebby will not be amused!

\section{Writing Code}

Basically, I'm writing QDOS and/or SMSQ routines to match those documented
in the Technical Guides. For example, Listing \ref{lis:FPC-FileOpen-function}
is the Pascal \texttt{FileOpen} function, which opens a file, this
is found in the \texttt{sysutils} unit for the QL.

\begin{lstlisting}[caption={FPC FileOpen function},label={lis:FPC-FileOpen-function}]
function FileOpen(const FileName: rawbytestring; Mode: Integer): THandle;
var
  QLMode: Integer;
begin
  FileOpen:=-1;
  case Mode of
    fmOpenRead: QLMode := Q_OPEN_IN;
    fmOpenWrite: QLMode :=  Q_OPEN_OVER;
    fmOpenReadWrite: QLMode := Q_OPEN;
  end;
  FileOpen := io_open(pchar(Filename), QLMode);
  if FileOpen < 0 then
    FileOpen:=-1;
end;


function FileGetDate(Handle: THandle) : Int64;
begin
  result:=-1;
end;

\end{lstlisting}

You can see, about half way down, there's a call to \texttt{io\_open},
passing the filename and the mode the file is to be opened. The code
for \texttt{io\_open} is shown in Listing ?, and is found in the \texttt{qdos.inc}
file. You will hopefully notice from the code that there are two io\_open
functions:
\begin{itemize}
\item \texttt{io\_open\_qlstr}: which opens a file using a QL style string
for the file name - a word count followed by the bytes of the string.
\item \texttt{io\_open}: Which is called from Pascal, and converts the Pascal
parameters passed, to suitable ones to call \texttt{io\_open\_qlstr}.
\end{itemize}
You might also notice a function called \texttt{FileGetDate}? That
one simply returns -1 because it has yet to be written for the QL.
And this is a gentle reminder that we need more people to get involved.

\begin{lstlisting}[caption={FPC Io\_open function},label={lis:FPC-Io_open-function}]
const
  _IO_OPEN = $01;
...

function io_open_qlstr(name_qlstr: pointer; mode: longint): Tchanid; assembler; nostackframe; public name '_io_open_qlstr';
asm
  movem.l d2-d3,-(sp)
  move.l name_qlstr,a0
  moveq.l #-1,d1
  move.l mode,d3
  moveq.l #_IO_OPEN,d0
  trap #2
  tst.l d0
  bne.s @quit
  move.l a0,d0
@quit:
  movem.l (sp)+,d2-d3
end;

function io_open(name: pchar; mode: longint): Tchanid; public name '_io_open';
var
  len: longint;
  name_qlstr: array[0..63] of char;
begin
  len:=length(name);
  if len > length(name_qlstr)-2 then
    len:=length(name_qlstr)-2;

  PWord(@name_qlstr)[0]:=len;
  Move(name^,name_qlstr[2],len);

  result:=io_open_qlstr(@name_qlstr,mode);
end;

\end{lstlisting}


\subsection{Parameter Passing}

The default method of passing parameters around in Free Pascal is
named \emph{register}. This method passes parameters using registers
where it can, as follows: 
\begin{itemize}
\item The first ordinal number (ie, a numeric value) is passed in register
\texttt{D0}.
\item The second ordinal number (ie, a numeric value) is passed in register
\texttt{D1}.
\item The first pointer or reference is passed in register \texttt{A0}.
\item The second pointer or reference is passed in register \texttt{A1}.
\item Any remaining parameters are passed on the stack from left to right.
\end{itemize}
If you look closely at \texttt{io\_open} you will see that it takes
the address of the QL String for the filename as a \texttt{pointer}
data type. This will be passed in register \texttt{A0} as per the
convention, however, the eagle-eyed among you will have spotted that
there is an instruction \lstinline[showstringspaces=false]!move.l name_qlstr,a0!
which, if the above information is correct, simply copies \texttt{A0}
to \texttt{A0}.

This is indeed correct, however, it should not be deleted as a spurious
line of code. There are other parameter passing modes, documented
on the \href{https://wiki.freepascal.org/m68k\#Registers}{Free Pascal Wiki here}\footnote{https://wiki.freepascal.org/m68k\#Registers},
which use a different manner of passing parameters. If you delete
that \emph{spurious} line, and the mode changes in future, code will
stop working.

Another problem that might bite you, is when you are usually a bit
anally retentive about setting up the registers in order, you might,
as I did, find yourself overwriting registers \texttt{D0} and \texttt{D1}
with QDOSSMSQ trap settings, for example, before you've grabbed the
parameters passed from Pascal. How do I know this? Don't ask -- but
it took a lot of \texttt{QMON}ing to find out what was going wrong!

\subsubsection{Returning Results}
\begin{itemize}
\item Return values which are 32 bits in size, or smaller numeric values
are returned in register \texttt{D0}.
\item Pointers are returned in register \texttt{A0} or \texttt{D0} (platform
and calling convention specific).
\item 64 bit ordinal values are returned in register pair \texttt{D0}/\texttt{D1}.
\end{itemize}
You can see the channel ID being returned from \texttt{io\_open\_qlstr}
in \texttt{D0} instead of the usual QDOSMSQ manner of returning it
in \texttt{A0}.

\section{Join In If You Can}

If you can write assembly code, then why not join in? Grab my document
and have a go at making one or more of the missing functions for the
QL, work. It's fun and a good way to be useful -- although my wife,
hello dear, begs to differ on my definition of \emph{useful}!

The main forum thread starts from \emph{chainq}'s post, linked above.
That's where I have been putting my patch files and they've been accepted
into the FPC project, some slightly amended by \emph{chainq}, which
is fine, he knows this stuff, I'm just a code monkey! There's also
a good deal of information on that thread about the way that FPC works
for the QL.

If you have questions, ask away, someone will chime in and hopefully,
help you out.

One more thing, the FPC project recently\footnote{Depending on when you read this, maybe not quite so recently!}
(8th August 2020) migrated everything from Subversion as a version
control system, to git. The project is now hosted on GitLab and mirrored
to gitHub, for safety, but GitLab is the main repository. You can
\href{https://gitlab.com/freepascal.org/fpc/source.git}{find it here}\footnote{https://gitlab.com/freepascal.org/fpc/source.git}.
