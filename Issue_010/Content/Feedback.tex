
\chapter{Feedback}

\section{Circular Buffers}

When I announce Issue 9 on the QL Forum, Marcel queried the Circular
Buffer article in the issue. He was wondering if I had simply recreated
the Queue handling features of QDOSMSQ, those being \texttt{IOQ.SETQ},
\texttt{IOQ.TEST}, \texttt{IOQ.PBYT}, and \texttt{IOQ.GBYT}. 

Putting it simply, \emph{probably}! However, as I pointed out, it
was a fun exercise in creating the code from a C++ version, and debugging
a particularly insidious bug where allocating common heap space gets
rounded up!

Thanks Marcel, at least I know one person read it!

\section{Learning Assembly Language}

\emph{TMD2003} wondered about learning Assembly Language and started
\href{https://qlforum.co.uk/viewtopic.php?f=3&t=3976}{a thread}\footnote{https://qlforum.co.uk/viewtopic.php?f=3\&t=3976}
on the QL Forum\footnote{Yes, I know, it's not really feedback on the previous issue of the
eMagazine, but I thought it was relevant.}. On the second page of the thread, \emph{Tinyfpga} issued this statement:

\emph{In Basic all I have to do start my journey into the programming
world is to:- Type into QD}

\begin{lstlisting}
OPEN #1,con_ 
OUTLN #1,310,60,50,300 
CLS #1 : BORDER #1,1,7 
INPUT #1,a$
\end{lstlisting}

\emph{I then save the text in a ram disk, press the execute button
and ,\textquotedbl hey presto\textquotedbl , I see an app on my
screen. What could be an easier introduction to programming?}

Well, I decided that that would be a good start to the new Beginner's
feature, so I've taken up \emph{Tinyfpga}'s ``challenge'' in this
issue and converted his \emph{easy introduction to programming} into
Assembly Language.

\section{Wolfgang's Feedback on Label Alignment}

Wolfgang Lenerz also mentioned a foible he has discovered in the way
I format my source code for Assembly Language programs and utilities.
This is quite a weird foible, in my opinion, but never the less it
needs looking into. Here are Wolfgang's comments:
\begin{itemize}
\item I downloaded the QL Assembly Language issue 9. As usual, it's fun
to read! I have a small comment regarding your label emplacements
- I hope you don't mind. Looking at the ql2win filter programs, it
starts like this:

\begin{lstlisting}
start
    bra.s checkStack
    dc.l $00
    dc.w $4afb

name
    dc.w name_end-name-2
    dc.b 'QL2WIN'
name_end equ *

version
    dc.w vers_end-version-2
    dc.b 'Version 1.00'
vers_end equ *

checkStack
    ...
\end{lstlisting}

Let's suppose I wanted to print out the version, I might start that
by using the lines:

\begin{lstlisting}
    lea version,a1
    move.w (a1)+,d1
    ...
\end{lstlisting}

somewhere in the code. That would work fine.

Now make the program name any number of odd characters, like 'QL2WINa'
and leave the rest as is. One would think that this shouldn't make
any difference.

Strangely enough though, when you run this, the line \lstinline[showstringspaces=false]!lea version,a1!
will give you an address error\footnote{Admittedly, you won't have a problem on \emph{QPC} - but that is because
\emph{QPC} emulates an 68020, which can handle word and longword reads/writes
at odd addresses. You won't have a problem with \emph{SMSQmulator}
either i.e. no address error exception, but you will get \textquotedbl unexpected
results\textquotedbl ...} as A1 will point to an odd address!

If you now move the \texttt{version} label to the line \texttt{dc.w
vers\_end-version-2}, everything will be fine again, whether the name
length is odd or even.

So what happens? The assembler (at least the macro assembler, I don't
know about George's) will go through the file and note the address
of the version label. This comes right after the bytes of the name,
and if the name length is uneven, that address is also odd. Then the
assembler continues and notes the \texttt{DC.W} directive. As this
would lie at an odd address, it inserts a filler byte - which comes
\emph{after} the \texttt{version} label.

If you now put the label on the same line as the \texttt{DC.W} directive,
the assembler puts in the filler byte before noting the address of
\texttt{version} label.

This is why there is no problem with leaving labels inside of actual
code even above the line the label refers to - code is always word
aligned. But if you start handling bytes, then you might get an odd
address at a label.

This is why most people leave more space between the beginning of
the line and the start of the opcode - it leaves more space for putting
the labels in front of them.
\end{itemize}
Interesting! I'll answer the last point first, I put labels on a separate
line because I prefer it that way. I was brought up on COBOL where
that was required. It has sort of stuck. I never realised it would
cause so much trouble though.

I decided to run a few tests, using \emph{GWASL}, \emph{GWASS} and
\emph{QMAC} which are the assemblers I own. The source code was as
follows:

\begin{lstlisting}
;    section code

start
    bra.s checkStack
    dc.l $00
    dc.w $4afb

name
    dc.w name_end-name-2
    dc.b 'QL2WIN'
name_end equ *

version
    dc.w vers_end-version-2
    dc.b 'Version 1.00'
vers_end equ *

start2
    bra.s checkStack
    dc.l $00
    dc.w $4afb

name2
    dc.w name_end2-name2-2
    dc.b 'QL2WIN'
name_end2 equ *

version2
    dc.w vers_end2-version2-2
    dc.b 'Version 1.00'
vers_end2 equ *

checkStack
    moveq #0,d0
    rts

;    end
\end{lstlisting}

I uncommented the \texttt{section code} and \texttt{end} lines to
compile with \emph{QMAC}. The results of my experimenting are shown
in table \ref{tab:Weird-Label-Addresses}.

\begin{table}[!h]
\begin{centering}
\begin{tabular}{|c|c|c|c|}
\hline 
 & \textbf{GWASL} & \textbf{GWASS} & \textbf{QMAC}\tabularnewline
\hline 
\hline 
\textbf{start} & \$0000 & \$0000 & \$0000\tabularnewline
\hline 
\textbf{name} & \$0008 & \$0008 & \$0008\tabularnewline
\hline 
\textbf{name\_end} & \$000D & \$000D & \$000D\tabularnewline
\hline 
\textbf{version} & \cellcolor{red}\$000D & \cellcolor{red}\$000D & \cellcolor{red}\$000D\tabularnewline
\hline 
\textbf{vers\_end} & \$001C & \$001C & \$001C\tabularnewline
\hline 
\hline 
\textbf{start2} & \$001C & \$001C & \$001C\tabularnewline
\hline 
\textbf{name2} & \$0024 & \$0024 & \$0024\tabularnewline
\hline 
\textbf{name\_end2} & \$0029 & \$0029 & \$0029\tabularnewline
\hline 
\textbf{version2} & \$002A & \$002A & \$002A\tabularnewline
\hline 
\textbf{vers\_end2} & \$0038 & \$0038 & \$0038\tabularnewline
\hline 
\end{tabular}
\par\end{centering}
\caption{Weird Label Addresses\label{tab:Weird-Label-Addresses}}

\end{table}

The odd addresses, that we care about, are highlighted. Just taking
a slightly edited extract from the \emph{GWASL}/\emph{GWASS} listing
files, we can see the problem:

\begin{lstlisting}
...
000D 00                        version
000E 000C                          dc.w vers_end-version-2
0010 5665 7273 696F 6E20 312E      dc.b 'Version 1.00'
001C                           vers_end-version-2
\end{lstlisting}

The label \texttt{version}, is at address \$000D, or 13 in decimal,
which is definitely odd. A similar output can be seen in the listing
file from \emph{QMAC}.

Well, that's a bummer indeed! How to resolve the issue? Simples!

\begin{lstlisting}
start
    bra.s checkStack
    dc.l $00
    dc.w $4afb

name
    dc.w name_end-name-2
    dc.b 'QL2WIN'
name_end equ *

	ds.w 0		; Force word alignment

version
    dc.w vers_end-version-2
    dc.b 'Version 1.00'
vers_end equ *

...

checkStack
    moveq #0,d0
    rts
\end{lstlisting}

By adding the line \lstinline[showstringspaces=false]!ds.w 0! to
force word alignment, the problem goes away. This is because the alignment
byte is inserted \emph{before} the label and not \emph{at} the label
as before. This extract from the \emph{GWASL}/\emph{GWASS} listing
files shows the fix in place:

\begin{lstlisting}
...

000E                           version
000E 000C                          dc.w vers_end-version-2
0010 5665 7273 696F 6E20 312E      dc.b 'Version 1.00'
001C                           vers_end-version-2
\end{lstlisting}

The version label is now correctly word aligned and has an even address.

\section{Wolfgang's Comments on \texttt{Ql2Win\_asm}}

Wolfgang also sent me a second email, after asking if he could comment
on my code, with the following comments and observations. 

I'm happy for any comments to be sent to me regarding anything I write,
I'm not an expert and sometimes there are other/better ways to do
what I do, feel free to point them out!
\begin{itemize}
\item I don't understand the reason for the code at label \texttt{gotLine}.
\end{itemize}
This may be down to my use of \texttt{IO\_FSTRG} which I was using
originally, instead of \texttt{IO\_FLINE} which I changed it to use
instead. I think, if I remember, I had a bug or two, or something
wasn't quite right with \texttt{IO\_FSTRG} hence the change. I might
have forgotten to clean up afterwards!
\begin{itemize}
\item You test for the presence of LF at the end of the string. But, in
your scheme, it will always be there!Well, only if the line was shorter
than the buffer length. 
\end{itemize}
Looking at the code (\texttt{ql2win\_asm}) I see that \texttt{gotLine}
looks for the linefeed, and if not there, skips to \texttt{putLine}
to write out the buffer, before reading the next chunk again by branching
back to \texttt{readLoop}.
\begin{itemize}
\item Indeed, if you get a line that is too long for the buffer, the trap
comes back with a buffer full error (-5) (or, sometimes, mistakenly,
with an overflow error, -18) - and in that case you abandon the treatment
as you leave with the error. So the only time you get to the label
is when the line does have the correct LF at the end - no need to
check for it.
\end{itemize}
This is \emph{obviously} (now you mention it) a bug. You are correct,
if the buffer is too small I will get an error, so I'll never be able
to read the next chunk of the partial line. Thanks for pointing this
out. 
\begin{itemize}
\item You should really also treat the \texttt{bffl}\footnote{\texttt{Buffer Overflow}}
error, by just writing the line so far, and then getting the rest
of it.
\end{itemize}
See above! I thought I was doing this, but the trap handling code
breaks it. Duh!
\begin{itemize}
\item When there is an EOF at the read trap, you leave w/o error. What if
the last file in the file didn't end with LF or CR/LF? You'll also
get an EOF, but there will be a remainder still in the buffer. Shouldn't
the remainder still be written out?
\end{itemize}
Yes, another bug that needs fixing. Thanks.
\begin{itemize}
\item Consider this code:
\begin{lstlisting}
(jump here from start)
checkStack
     cmpi.w  #numchans,(a7)    ; Two channels is a must
     beq.s   ql2win            ; Ok, skip error bit

bad_parameter
     moveq   #err_bp,d0        ; Guess!
     bra     errorExit         ; Die horribly

ql2win
     moveq   #timeout,d3       ; Timeout
\end{lstlisting}

After checking for the correct number of channels, you branch to \texttt{ql2win}
if that is ok, else you continue and treat the error. May I suggest
you do the contrary? Check for correct number of channels and branch
to the error code if there is a problem, else continue normally. Something
like this:
\begin{lstlisting}
bad_parameter
     moveq   #err_bp,d0        ; Guess!
     bra     errorExit         ; Die horribly

(jump here from start)
checkStack
     cmpi.w  #numchans,(a7)    ; Two channels is a must
     bne.s   bad_parameter     ;

ql2win
     moveq   #timeout,d3       ; Timeout
\end{lstlisting}

The reason is two-fold.

First of all, a branch not taken is faster than a branch taken. So
here, you only take the branch if there is a problem with the parameters,
and in that case you don't really care if that takes a microsecond
more.

Second, you don't interrupt the program flow for whoever is reading
your program. Instead of also having to (mentally) jump to the \texttt{ql2win}
label, you just continue reading. (Admittedly, this last point is
more of a personal preference).

The same reasoning could also be applied in the \texttt{readloop},
after the trap where you test \texttt{D0} for errors - only branch
if there is an error, not if there isn't.
\end{itemize}
All of which makes perfect sense, thanks. I usually try to consider
the most often occurring case first, but occasionally, I do things
in a different order. I must try harder.
\begin{itemize}
\item Maybe you should tell your readers that you don't explicitly close
the channels that were opened, since the OS automatically closes all
channels opened (or rather owned) by a job when that job is removed.
\end{itemize}
A good suggestion, thanks. I will. In fact, I'll do it now!

I have omitted to make clear that any open channels, chunks of heap
space allocated to a specific job, will be reclaimed by the operating
system when the job is removed from the system, either by itself or
forcibly by another job. This keeps things nice and tidy and is rather
helpful, \emph{however}, there's no harm whatsoever in closing files
yourself and returning allocated heap space back to the Common Heap.
\begin{itemize}
\item Finally, I presume that you didn't use the SMSQE queue handling (at
smsq\_ioq\_) for didactic reasons for your circular buffer (which
is what an SMSQE queue is).
\end{itemize}
Ah yes, the QDOS/SMSQ/E Queue Handling functions! Marcel mentioned
those (on the QL Forum) as well. I was aware of those, but this is
some code I've written in C++ for a project, and I will soon (for
certain values of soon) be converting it to Atmel AVR Assembly Language
for a similar project. I thought I would try it out on a language
I'm more familiar with first. It was also a fun project to work with,
having been away from the keyboard for a while.
\begin{itemize}
\item I know these were a great number of comments, but they wouldn't exist
if you hadn't taken it upon yourself to write your greatly enjoyable
series!
\end{itemize}
Comments are \emph{always} welcome, thanks. It's good to hear that
I have at least two readers! (Not counting myself, and also, why do
I always find bugs/typos after sending out the eMagazine!)

Thanks Wolfgang.

\section{Bug Fixes for Ql2Win/Win2ql}

\subsection{Ql2Win}

The changes to the source code for the bugs identified by Wolfgang
are documented below.

To fix the buffer overflow problem identified by Wolfgang, add two
extra equates near line 29, which currently reads as:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
err_bp      equ     -15
err_eof     equ     -10
me          equ     -1
\end{lstlisting}

Change it to this instead:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
err_bp      equ     -15
err_eof     equ     -10
err_bffl    equ     -5
err_ovfl    equ     -18
me          equ     -1
\end{lstlisting}

We have added, in SMSQ/E nomenclature, \texttt{err\_bffl} and \texttt{err\_ovfl}
as Wolfgang has noted that occasionally, the error returned in \texttt{err\_ovfl}
instead of \texttt{err\_bffl}. We will use these equates shortly,
but first, let's fix the buffer size problem. Around line 83, you'll
find this code:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
ql2win
    moveq   #timeout,d3       ; Timeout
    moveq   #buffSize,d4      ; Storage for buffer size for D2
    lea     buffer,a3         ; Start of (write) buffer
\end{lstlisting}

The problem, identified by Wolfgang, is the \texttt{moveq \#buffsize,d4}
instruction. Any buffer over 127 bytes will be sign extended to a
long word. This would make the lower word \$FF80 in the case of 128
byte buffers, and as the buffer size is assumed to be positive, this
makes for an assumed buffer size of 65,408 bytes rather than the actual
128. As soon as a line longer than 128 is read, bang!

The \texttt{moveq} should be changed to a \texttt{move.w} as in the
following:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
ql2win
    moveq   #timeout,d3       ; Timeout
    move.w  #buffSize,d4      ; Storage for buffer size for D2
    lea     buffer,a3         ; Start of (write) buffer
\end{lstlisting}

Right then, back to the buffer overflow problems. The code starting
at line 102 is where we need to test for the two new error codes.
It currently looks like this:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
readLoop
    moveq   #io_fline,d0      ; Fetch lines ending with LF
    move.w  d4,d2             ; Buffer size
    movea.l a4,a0             ; Channel to read
    movea.l a3,a1             ; Read buffer start
    trap    #3                ; Read a line from input file
    tst.l   d0                ; OK?
    beq.s   gotLine           ; Yes
    cmpi.l  #ERR_EOF,d0       ; All done yet?
    beq     allDone           ; Yes.
    bra     errorExit         ; Oops!
\end{lstlisting}

We need to change it to this instead:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
readLoop
    moveq   #io_fline,d0      ; Fetch lines ending with LF
    move.w  d4,d2             ; Buffer size
    movea.l a4,a0             ; Channel to read
    movea.l a3,a1             ; Read buffer start
    trap    #3                ; Read a line from input file
    tst.l   d0                ; OK?
    beq.s   gotLine           ; Yes
    cmpi.l  #ERR_EOF,d0       ; All done yet?
    bne.s   overflow          ; Not yet
    tst.w   d1                ; Did we read anything?
    bne.s   gotLine           ; Yes, deal with it
    beq     allDone           ; All done now

overflow
    cmpi.l  #ERR_BFFL,d0      ; Buffer overflow?
    beq.s   gotLine           ; Yes, write it out unchanged
    cmpi.l  #ERR_OVFL,d0      ; Buffer overflow (apparently!)
    beq.s   gotLine           ; Yes, write it out unchanged   
    bra     errorExit         ; Oops!
\end{lstlisting}

The observant among us -- which usually excludes myself -- will
notice that I test for any input even when we hit end of file. This
helps deal with Wolfgang's over observation that we can lose the last
line of the input file if it doesn't fill the buffer.

It will not have a CR/LF added though, if it doesn't have one when
read. If it comes without a QL line terminator, it goes out without
a Windows one as well. Seems fair to me?

With these changes, and a suitable test file, the code works as good
as it did before. All lines, no matter how long, will be correctly
written out and converted to Windows format line endings.

I've included a fixed version of the code in the code repository for
this issue, so that you don't have to fix it yourself!

\subsection{Win2ql}

As with ql2win, add two extra equates near line 29, which currently
reads as:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
err_bp      equ     -15
err_eof     equ     -10
me          equ     -1
\end{lstlisting}

Change it to this instead:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
err_bp      equ     -15
err_eof     equ     -10
err_bffl    equ     -5
err_ovfl    equ     -18
me          equ     -1
\end{lstlisting}

We have added \texttt{err\_bffl} and \texttt{err\_ovfl} as previously.
The buffer size problem is next in the source, around line 83, you'll
find this code:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
win2ql
    moveq   #timeout,d3       ; Timeout
    moveq   #buffSize,d4      ; Storage for buffer size for D2
    lea     buffer,a3         ; Start of (write) buffer
\end{lstlisting}

Once again, the \texttt{moveq} should be changed to a \texttt{move.w}
as in the following:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
win2ql
    moveq   #timeout,d3       ; Timeout
    move.w  #buffSize,d4      ; Storage for buffer size for D2
    lea     buffer,a3         ; Start of (write) buffer
\end{lstlisting}

Back to the buffer overflow problems. The code currently starting
at line 100 is where we need to test for the two new error codes.
It currently looks like this:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
readLoop
    moveq   #io_fline,d0      ; Fetch lines ending with LF
    move.w  d4,d2             ; Buffer size
    movea.l a4,a0             ; Channel to read
    movea.l a3,a1             ; Read buffer start
    trap    #3                ; Read a line from input file
    tst.l   d0                ; OK?
    beq.s   gotLine           ; Yes
    cmpi.l  #ERR_EOF,d0       ; All done yet?
    beq     allDone           ; Yes.
    bra     errorExit         ; Oops!
\end{lstlisting}

We need to change it to this instead:

\begin{lstlisting}[showstringspaces=false,tabsize=4]
readLoop
    moveq   #io_fline,d0      ; Fetch lines ending with LF
    move.w  d4,d2             ; Buffer size
    movea.l a4,a0             ; Channel to read
    movea.l a3,a1             ; Read buffer start
    trap    #3                ; Read a line from input file
    tst.l   d0                ; OK?
    beq.s   gotLine           ; Yes
    cmpi.l  #ERR_EOF,d0       ; All done yet?
    bne.s   overflow          ; Not yet
    tst.w   d1                ; Did we read anything?
    bne.s   gotLine           ; Yes, deal with it
    beq     allDone           ; All done now

overflow
    cmpi.l  #ERR_BFFL,d0      ; Buffer overflow?
    beq.s   gotLine           ; Yes, write it out unchanged
    cmpi.l  #ERR_OVFL,d0      ; Buffer overflow (apparently!)
    beq.s   gotLine           ; Yes, write it out unchanged   
    bra     errorExit         ; Oops!
\end{lstlisting}

This is pretty much the same changes as for ql2win. I test for any
input even when we hit end of file, as before, so that when the last
line of a file doesn't fill the buffer it will still be written out.

As with ql2win, it will not have a QL line ending LF added if there
wasn't one presnet when the line was read. If it comes without a Windows
line terminator, it goes out without a QL one as well. Seems fair
to me?
